hyh

medium

# SLASHER can grief the stakers by reducing the withdrawing window or increasing delay

## Summary

SLASHER can manipulate both window and delay within the bounds, which will affect the stakers already requested the withdrawal.

## Vulnerability Detail

Suppose there is a room for `withdrawalDelay` increase or `withdrawalWindow` decrease, let's say `minWithdrawalWindow = 5 days`, `withdrawalWindow = 7 days`, so it can be decreased, and Bob the staker requested the withdrawal earlier, now wants to withdraw, while it is exactly `2 days - 1 second` left for him to do so.

SLASHER can front-run Bob's exit() call and reduce the `withdrawalWindow` to be `5 days`, effectively reverting Bob's withdrawal. SLASHER can set `withdrawalWindow = 7 days` immediately after that and then repeat the front-running if needed.

## Impact

Users can be denied withdrawals for the additional periods they don't expect. That mean temporary fund freeze for them, i.e. they can rely on the boundary values only, but think that can rely on the settings as of the time of withdraw request.

For example, if there is some other Telcoin staking pools with higher yield, which Bob wants to use, but can't as withdrawal is denied this way for him, then impact ends up to be the loss of the corresponding additional yield.

## Code Snippet

SLASHER sets the withdrawal delay and window with immediate effect:

https://github.com/sherlock-audit/2023-02-telcoin/blob/main/telcoin-audit/contracts/staking/StakingModule.sol#L515-L526

```solidity
    /// @notice Sets the withdrawal delay and window to prevent frontrunning slashes
    function setWithdrawDelayAndWindow(uint256 delay, uint256 window) external onlyRole(SLASHER_ROLE) {
        require(delay <= maxWithdrawalDelay, "StakingModule: Desired delay is too long");
        require(window >= minWithdrawalWindow, "StakingModule: Desired window is too short");

        // if window + delay is >= current time, then someone with a requestTimestamp of 0 will be able to withdraw (i.e. withdraw without requesting)
        // when requestTimestamp = 0, the following must hold: t > d + w
        require(window + delay < block.timestamp, "StakingModule: Desired window + delay is too large");

        withdrawalDelay = delay;
        withdrawalWindow = window;
    }
```

But this effect applies to ones who requested the withdrawal at the old terms:

https://github.com/sherlock-audit/2023-02-telcoin/blob/main/telcoin-audit/contracts/staking/StakingModule.sol#L115-L124

```solidity
    modifier delayedWithdrawal() {
        require(withdrawalDelay == 0 || (
                withdrawalRequestTimestamps[msg.sender] + withdrawalDelay <= block.timestamp &&
                block.timestamp <= withdrawalRequestTimestamps[msg.sender] + withdrawalDelay + withdrawalWindow
            ), 
            "StakingModule: Withdrawal not requested yet or it is too early/late to withdraw"
        );
        withdrawalRequestTimestamps[msg.sender] = 0;
        _;
    }
```

## Tool used

Manual Review

## Recommendation

Consider saving two times for `msg.sender`, withdrawal start and end, so they be fixed and a user can count on it:

https://github.com/sherlock-audit/2023-02-telcoin/blob/main/telcoin-audit/contracts/staking/StakingModule.sol#L63

```diff
-   mapping(address => uint256) public withdrawalRequestTimestamps;
+   mapping(address => uint256) public withdrawalAllowedStart;
+   mapping(address => uint256) public withdrawalAllowedEnd;
```

https://github.com/sherlock-audit/2023-02-telcoin/blob/main/telcoin-audit/contracts/staking/StakingModule.sol#L115-L124

```diff
    modifier delayedWithdrawal() {
        require(withdrawalDelay == 0 || (
-               withdrawalRequestTimestamps[msg.sender] + withdrawalDelay <= block.timestamp &&
-               block.timestamp <= withdrawalRequestTimestamps[msg.sender] + withdrawalDelay + withdrawalWindow
+               withdrawalAllowedStart[msg.sender] <= block.timestamp && block.timestamp <= withdrawalAllowedEnd[msg.sender]
            ), 
            "StakingModule: Withdrawal not requested yet or it is too early/late to withdraw"
        );
        withdrawalRequestTimestamps[msg.sender] = 0;
        _;
    }
```

https://github.com/sherlock-audit/2023-02-telcoin/blob/main/telcoin-audit/contracts/staking/StakingModule.sol#L229-L236

```diff
    /// @notice Request a withdrawal if withdrawalDelay is nonzero
    /// @dev This is required to prevent users from frontrunning slashing
    function requestWithdrawal() external {
        require(withdrawalDelay > 0, "StakingModule: Withdrawal delay is 0");
-       require(block.timestamp > withdrawalRequestTimestamps[msg.sender] + withdrawalDelay + withdrawalWindow, "StakingModule: Withdrawal already pending");
+       require(block.timestamp > withdrawalAllowedEnd[msg.sender], "StakingModule: Withdrawal already pending");

-       withdrawalRequestTimestamps[msg.sender] = block.timestamp;
+       withdrawalAllowedStart[msg.sender] = block.timestamp + withdrawalDelay;
+       withdrawalAllowedEnd[msg.sender] = block.timestamp + withdrawalDelay + withdrawalWindow;
    }
```